plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.4'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.jfoenix:jfoenix:9.0.10'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'com.profesorfalken:jPowerShell:3.1.1'
    implementation 'org.json:json:20131018'
    implementation 'org.apache.ftpserver:ftpserver:1.1.1'
    implementation project(':Shared')
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
    version = '15.0.1'
}

application {
    mainClass.set("application.Launcher")
    applicationName = 'OpenRAMT'
}

run {
    jvmArgs = [
            "--add-exports=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--illegal-access=permit"
    ]
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        noConsole = true
        jvmArgs = [
                "--add-exports=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
                "--illegal-access=permit"
        ]
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/openramt.$imgType"]
        appVersion = "0.0.1"
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Nex']
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', applicationName,'--linux-shortcut']
            installerType = 'msi'
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', applicationName]
        }
    }
}